defaultTasks 'clean', 'check', 'publishToMavenLocal'

subprojects {

  group='com.codeborne.replay'
  version=project.properties['revision'] ?: '1.9.0'

  apply plugin: 'java'
  apply plugin: 'java-library'

  [compileJava, compileTestJava]*.options.collect {options ->
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
  }
  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    mavenCentral()
    mavenLocal()
  }

  sourceSets {
    main {
      java {
        srcDir 'src'
        include '**/*.java'
      }
      resources {
        srcDir 'src'
        include '**/*.html'
        include '**/*.json'
        include '**/*.xml'
        include '**/*.tag'
        include '**/*.txt'
        include '**/*.properties'
        include 'META-INF/**/*'
      }
    }
    test {
      java {
        srcDir 'test'
        include '**/*.java'
      }
      resources {
        srcDir 'test'
        include '**/*.sql'
        include '**/*.tag'
        include '**/*.txt'
        include '**/*play*plugins*'
        include '**/*.xml'
        include '**/*.properties'
        include 'META-INF/**/*'
      }
    }
  }


  test {
    include 'play/**/*'
    systemProperties['file.encoding'] = 'UTF-8'
  }

  tasks.withType(Test).all { testTask ->
    testTask.systemProperties['file.encoding'] = 'UTF-8'
    testTask.testLogging {exceptionFormat = 'full'}
    testTask.outputs.upToDateWhen { false }
  }
}
