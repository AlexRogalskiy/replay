import java.nio.file.*

defaultTasks 'clean', 'test', 'install'

subprojects {

  group='com.codeborne.replay'
  version=project.properties['revision'] ?: '1.7-SNAPSHOT'

  apply plugin: 'java'
  apply plugin: 'maven'

  [compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
  [compileJava, compileTestJava]*.options.collect {options -> options.debug = true}
  compileJava.options.debugOptions.debugLevel = "source,lines,vars"
  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }

  sourceSets {
    main {
      java {srcDir 'src'}
      resources {srcDir 'src'}
    }
    test {
      java {srcDir 'test'}
      resources {srcDir 'test'}
    }
  }

  classes.dependsOn ':deps'

  test {
    include 'play/**/*'
    systemProperties['file.encoding'] = 'UTF-8'
  }

  jar {
    from sourceSets.main.allSource

    manifest {
      attributes(
        "Implementation-Title": project.group + '.' + project.name,
        "Implementation-Version": version,
        "Implementation-Vendor": "Codeborne")
    }
  }
}

def fetchDependencies(destination, dependencies) {
  delete destination
  new File(destination).mkdirs()
  dependencies.grep{it.name.endsWith(".jar")}.toSet().each {
      Files.createSymbolicLink(Paths.get("${destination}/${it.name}"), Paths.get(it.path));
  }
}

task deps {
  doLast {
    def compileDependencies = subprojects.collect{it.configurations.compile.collect{it}}.flatten()
    def testDependencies = subprojects.collect{it.configurations.testRuntime.collect{it}}.flatten() - compileDependencies
    fetchDependencies("${rootDir}/build/lib", compileDependencies)
    fetchDependencies("${rootDir}/build/lib-test", testDependencies)
  }
}

task integrationTests(type: GradleBuild) {
  buildFile = 'replay-tests/build.gradle'
  tasks = ['test', 'uitest']
}
